Constant Story "9:05";
Constant Headline
  "^by Adam Cadre^
  Перевод: Александр Мосьпан. Порт: Всеволод Зубарев.^^";

Include "Parser";

Object LibraryMessages
  with
  before [;
    Attack: "Это пустая трата сил.";
    Push, Pull: "Это ни к чему не приводит.";
    Sleep: "Ты и так проспал слишком долго!";
  ];

Include "VerbLib";

Statusline time;

Global PlayerClean;

[ boldstring str;
  style bold;
  print (string) str;
  style roman;
];

[ Pause k;
  ! todo: восстановить функцию паузы
  ! @read_char 1 -> k;
  rtrue;
];

[ Bowman_room;
  if (form has general && form in player) {
    print "Ты входишь в офис Боумэна, и, улыбаясь, протягиваешь ему подписанный бланк.
    ^^«А ты ещё кто такой?», — спрашивает Боумэн.
    ^^Ты попался. Ты пытаешься бежать, но Боумэн вызывает охрану и они заламывают тебе руки прежде чем ты успеваешь добежать до машины.^^";
  	! Pause();
    ! todo: восстановить очистку экрана
    ! @erase_window -1;
    print "^^^Телеведущий уcмехается. «Теперь перейдём к абсурдным новостям. Вы, конечно, слышали о грабителе, который проник в квартиру, собрал все фамильные драгоценности в мешок, и так утомился, что заснул на месте преступления, где его и нашли по возвращении хозяева квартиры. Наш сегодняшний герой превзошёл его. В Лас Мезасе грабитель забрался в дом, убил хозяина и спрятал тело под кровать, после чего сложил его имущество в багажник своей машины, и отправился спать. Проснувшись на утро, он явился в офис жертвы, и попытался выполнять его работу, по-видимому, пытаясь сойти за сумасшедшего. Новости погоды вам представит Джеф Даблдэй».^^^",
    (boldstring) "*** Тебя приговаривают к пожизненному заключению. ***";
    deadflag = true;
  }
  else
    print "Идти в офис Боумэна без подписанного бланка? Не очень умно.^";
];

[ UseExtraSub;
  "Это нельзя использовать.";
];

[ SignSub;
  if (pen in player)
    "Ты проводишь ручкой по ", (cDat) noun, ", но она не оставляет следа.";
  else
    "Тебе нечем подписать ", (cAcc) noun, ".";
];

[ WipeSub;
  if (location ~= bathroom)
    "Тебе нечем умыться.";
  if (PlayerClean == 0)
    "Сейчас тебе может помочь только душ.";
  else
    "Ты уже чистый.";
];

[ PlayerDesc;
  if (PlayerClean == 0)
    "Ты покрыт грязью и засохшим потом. Это была изматывающая ночка — неудивительно, что ты проспал! Даже то, что ты добрался до постели, прежде чем вырубиться — уже огромное достижение.";
  else
    "После душа ты выглядишь гораздо лучше.";
];

!Object -> -> thief "(thief)"
! with name 'mud' 'dried' 'sweat',
!  description "    ysc",
!  !number [3 e7],
!has animate proper;

Object soiled_clothing "грязн/ая одежд/а"
  with name 'грязн' 'одежд',
  before [;
    Wear: if (PlayerClean) "Надевание грязной одежды на чистое тело уничтожит весь смысл принятия душа.";
  ],
  description [;
    "Эта одежда безнадёжно испорчена. Ни одна прачечная не сможет отстирать ", (boldstring) "эти", " пятна.";
  ],
  has clothing worn female;

Object gold_watch "золот/ые час/ы"
  with name 'золот' 'час',
  description [;
    print_ret "На твоих часах ", the_time / 60, ":", (the_time % 60) / 10, the_time % 10, ".";
  ],
  has clothing worn pluralname;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Спальня
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Object Bedroom "Спальня"
  with s_to Bathroom,
  e_to _door,
  description "Спальня очень скромная, грязное бельё раскидано по полу. Чистая одежда, должно быть, висит в шкафу.
  Ванная находится на юге, а восточная дверь ведёт в гостиную.",
  before [;
    Smell: "В комнате воняет мертвечиной";
  ]
  has light;

Object -> dirty_laundry "грязн/ое бель/е"
  with name 'грязн' 'бель',
  before [;
    Take: "Оно не чище чем то что на тебе.";
  ],
  description "Майки, трусы. Как обычно.",
  has clothing scenery neuter;

Object -> dresser "шкаф/"
  with name 'шкаф' 'платян',
  before [;
    Push, Pull, Take: "Он слишком тяжёлый.";
  ],
  description "Обычный платяной шкаф.",
  has container openable scenery male;

Object -> -> clean_clothing "чист/ая одежд/а"
  with name 'чист' 'одежд',
  before [;
    Disrobe: "Сейчас не время переодеваться.";
    Wear: if (PlayerClean == 0) "Глупо надевать чистую, свежую одежду, пока тебя покрывает грязь и засохший пот.";
  ],
  description "Не очень стильная, но презентабельная.",
  has clothing female;

Object -> bed "кроват/ь"
  with name 'кроват',
  before [;
    LookUnder: "Под кроватью ты видишь труп парня — владельца этого дома.";
  ],
  description "Обычная кровать.",
  has enterable scenery supporter female;

Object -> endtable "стол/"
  with name 'стол' 'дубов' 'шпон' 'фанер',
  description "Небольшой столик. Дубовый шпон на многослойной фанере.",
  has scenery supporter male;

Object -> -> telephone "телефон/"
  with name 'телефон' 'трубку',
  before [;
    Take:
      if (self hasnt general) {
        give self general;
        remove phonemessage;
        "Ты снимаешь трубку. «Хэдли! — кричит срывающийся голос. — Хэдли, ты до сих пор не выехал?! Ты ведь знаешь, что наша презентация должна была начаться ровно в девять! Сначала ты подставил нас перед типографией, теперь это. Боумэн очень хочет тебя видеть, так что живо собирайся и гони сюда!»^^
        Короткие гудки.^^
        Ты кладёшь трубку.";
      } else
        "Он больше не звонит.";
  ],
  description [;
  print "Обычный телефон, выдающийся только тем, что ему уже пятнадцать или двадцать лет.^";
    if (self hasnt general) "^Телефон звонит.";
  ],
  has male;

Object -> -> wallet "бумажник/"
  with name 'бумажник' 'кошелёк',
  before [;
    Receive:
      if (noun ~= driver_s_license || noun ~= ID_card)
        "", (CCNom) noun, " не поместится в бумажник.";
  ],
  description "Бумажник из коричневой кожи.",
  has container openable male;

Object -> -> -> driver_s_license "водительск/ие прав/а"
  with name 'прав' 'водительск',
  description "БРАЙАН ХЭДЛИ^
    АВЕНЮ ВИКТОРИЯ 315^
    ЛАС МЕЗАС, #88827^^
    Фотографии на водительские правах обычно получаются далеко не лестными, и эта не стала исключением, но даже она выглядит намного лучше, чем ты сейчас.",
  has pluralname;

Object -> -> -> ID_card "идентификационн/ая карточк/а"
  with name 'идентификационн' 'карточк' 'магнитн' 'полоск' 'полос',
  description "По размеру и форме напоминает кредитную карту, но на обратной стороне у неё магнитная полоска, а на лицевой надпись:^^
    БРАЙАН ХЭДЛИ^
    ЛАУНГЕНТ ТЕХНОЛОДЖИЗ^
    КОЛЛИНС СТРИТ 215^
    ЛАС МЕЗАС, #88701",
  has female;

Object -> -> keys "ключ/и"
  with name 'ключ' 'кольц',
  description "Ключи от дома и от машины на одном кольце.",
  has pluralname;

Object -> phonemessage "(phonemessage)"
  with describe "^Телефон звонит.",
  has;

Object -> _door "двер/ь"
  with name 'двер',
  door_to Living_room,
  door_dir e_to,
  description "Обычная дверь.",
  has door open openable scenery female;

Object -> corpse "труп/"
  with name 'труп' 'тело' 'парня' 'браян' 'хэдли',
  before [;
    Examine:;
    default: "Лучше не трогать труп — просто привести себя в порядок и смыться отсюда.";
  ],
  description "Ты совсем не жестокий. Обычно даже не берёшь с собой оружие. Но этот парень просто не оставил выбора, ринувшись на тебя с ножом, поэтому тебе ничего не оставалось, кроме как сломать ему шею.",
  has scenery male;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Ванная
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Object Bathroom "Ванная"
  with n_to Bedroom,
  out_to Bedroom,
  description "Не роскошная, но вполне функциональная ванная с умывальником, туалетом и душем. Вход в спальню находится на севере.",
  has light;

Object -> toilet "туалет/"
  with name 'туалет' 'унитаз',
  before [;
    Enter:
      if (self hasnt general) {
        give self general;
        "Хорошая идея. Через некоторое время, слив воду, ты чувствуешь себя намного лучше.";
      }
      else
        "Тебе уже не нужно.";
  ],
  description "Обычный туалет.",
	has open openable scenery male;

Object -> sink "умывальник/"
  with name 'умывальник' 'раковина',
  description "Ничего особенного.",
  has scenery male;

Object -> shower "душ/"
  with name 'душ' 'душев' 'кабинк',
  before [;
    UseExtra, Enter:
    if (soiled_clothing has worn)
      "Было бы неплохо раздеться, прежде чем принимать душ.";
    if (children(player) ~= 0)
      "Нести свои вещи под душ — это плохая идея.";
    if (PlayerClean == 0) {
    	PlayerClean = 1;
      "Ты становишься под душ, включаешь воду, и через несколько мгновений чувствуешь себя обновлённым.
      Но нельзя тратить время — ты выпрыгиваешь обратно, и моментально обсыхаешь.";
    }
    else
      "Ты уже чистый.";
  ],
  description "Вертикальная душевая кабинка без ванны.",
  has scenery male;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Гостиная
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Object Living_room "Гостиная"
  with description "Мебель расставлена не так красиво, как могла бы, и явно не хватает предметов, которые помогли бы приятно скоротать вечер — нет стереосистемы, телевизора, DVD-проигрывателя, ничего из электроники. Но всё равно эта комната лучше, чем большинство в этом районе. Кухня расположена на востоке, спальня — на западе. Входная дверь — на юге.",
  s_to [;
    if (PlayerClean == 0)
      "Тебе нужно почиститься. Сейчас ты выглядишь как потерпевший кораблекрушение. Если выйти на улицу в таком виде, любопытные взгляды только усугубят и без того плохую ситуацию.";
    if (clean_clothing hasnt worn)
      "Выходить на улицу голым, как минимум, неумно.";
    else
      return front_door;
  ],
  e_to Kitchenette,
  w_to bedroom_door,
  has light;

Object -> bedroom_door "двер/ь спальни"
  with name 'спальн' 'двер',
  door_to Bedroom,
  door_dir w_to,
  description "Обычная дверь.",
  has door open openable scenery female;

Object -> front_door "входн/ая двер/ь"
  with name 'входн' 'двер',
  before [;
    Lock: "Замок, похоже, сломан.";
  ],
  door_to Driveway,
  door_dir s_to,
  description "Эта дверь видела лучшие времена.",
  has door lockable openable scenery female;

Object -> lock "зам/о/к"
  with name 'замок',
  casegen [
    beg end csID;
  	return ICVowel (csID, beg, end, 'о', 0);
  ],
  before [;
  	Lock: <<Lock front_door>>;
  ],
  description "Замок сломан.",
  has scenery male;

Object -> furniture "мебел/ь"
  with name 'мебел' 'диван' 'стол' 'столик' 'стул' 'стулья',
  before [;
    Enter: "На это нет времени.";
  ],
  description "Диван, столики, несколько стульев.",
  has scenery female;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Кухня
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Object Kitchenette "Кухня"
  with w_to Living_room,
  out_to Living_room,
  description "У тебя совсем нет время искать еду и готовить себе вкусный завтрак прямо сейчас. Лучше действовать в темпе. Выход на западе.",
  has light;

Object -> counter "стойк/а"
  with name 'стойк',
  description "Огнеупорная пластмасса.",
  has scenery supporter female;

Object -> -> toaster "тостер/"
  with name 'тостер',
  before [;
    Receive:
      if (noun == pop_tart && pop_tart hasnt general) {
				give pop_tart general;
        "Ломтик разогревается и симпатично подрумянивается. Пару секунд ты ждёшь, пока он остынет, и достаёшь из тостера.";
      }
      if (noun == pop_tart)
        "Так ты просто сожжёшь его.";
      else
        "Тостер не игрушка.";
    SwitchOn, SwitchOff:
      "Он включается и выключается автоматически.";
  ],
  description "Простой тостер.",
  has male;

Object -> -> pop_tart "ломтик/ хлеба"
  with name 'ломтик' 'хлеб' 'булочк' 'кусочек' 'кусочк',
  after [;
    Eat: "Не воскресный ланч в ресторане, конечно, но и так сойдёт.";
  ],
  description [;
    if (self has general)
      "Кусочек булочки с корицей, поджаренный в тостере.";
    else
      "Кусочек булочки с корицей. Свежий.";
  ],
  has edible male;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Дорога у дома
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Object Driveway "Дорога"
  with n_to Living_room,
 ! with s_to parking_lot, !ВРЕМЕННО
  description "Это очень бедный район, хотя на самом деле, все районы Лас Мезаса сложно назвать благополучными. Конечно, местные жители могут жаловаться на мусор и преступность, но зато дом здесь стоит раз в пять дешевле, чем точно такой же где-нибудь в Лас Плэйсе — и то, сильно повезёт, если его найдёте.",
  before [;
    Drive:
      if (player in car) {
        PlayerTo (Driving);
        move car to parking_lot;
        rtrue;
      }
  ],
  has light;

Object -> car "машин/а"
  with name 'машин' 'авто' 'автомобил',
  before [;
    Open, Unlock:
      if (noun == keys) {
        "Ты открываешь дверь машины.";
      }
    
    Enter:
      if (keys notin player) {
        "Машина заперта.";
      }
      if (self in parking_lot) {
        print "Ты садишься обратно за руль и возвращаешься к дому...^";
        move self to Driveway;
        PlayerTo (Driveway);
        rtrue;
      }
  ],
  after [;
    Enter: "Ты открываешь дверь машины, забираешься внутрь, и заводишь двигатель.";
  ],
  description "Очень неплохая машина для такого района. Она прямо притягивает взгляд.",
  describe [;
    if (self in Driveway) "^Машина припаркована на дороге.";
    else rtrue;
  ],
  with_key keys,
  has enterable lockable locked transparent female;

Object -> trunk "багажник/"
  with name 'багажник',
  before [;
    Open: <<Examine self>>;
  ],
  description "Ты бросаешь быстрый взгляд на содержимое багажника. Телевизор, стереосистема, DVD-плеер — довольно впечатляющий улов.",
  found_in parking_lot Driveway,
  has scenery male;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Дорога
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Object Driving "Поездка"
  with
  driving_meter 0,
  description "Ах, пейзажи Лас Мезаса. Этот город больше похож на помойку. Скоро ты сможешь позволить себе свалить отсюда. Конечно, если больше не допустишь промахов в работе.",
  each_turn [;
    switch (self.driving_meter++) {
      0: print "^Проезжая по улице, ты приближаешься к выезду на автостраду. Собираешься ли ты свернуть на неё? >>";
      switch (YesOrNo()) {
        true:
          "^Ты начинаешь перестраиваться, но замечаешь, что сегодня проезд закрыт — какие-то дорожные работы.
          Но всё в порядке — меньше чем в миле отсюда есть ещё один выезд.";
        false:
          "^Оказалось, что выезд всё равно закрыт — какие-то дорожные работы.";
      }
      1: print "^Проезжая по улице, ты приближаешься к знакомому зданию Лаунгент Технолоджиз. Остановиться? >>";
      switch (YesOrNo()) {
        true:
          print "^Ты въезжаешь на стоянку, и паркуешься.^";
          PlayerTo (parking_lot);
          self.driving_meter = 0;
          rtrue;
        false:
          "^Вскоре здание остаётся позади.";
      }
      2: print "^Проезжая по улице, ты приближаешься к выезду из города. Собираешься ли ты свернуть на него? >>";
      switch (YesOrNo()) {
        true:
          print "^Ты сворачиваешь на автостраду, включаешь радио, и исчезаешь без следа.",
          (boldstring) "^^*** Ты покидаешь Лас Мезас ***";
          deadflag = true;
        false:
          print "^Ты решаешь не сворачивать на автостраду, но какой-то идиот на соседней полосе тоже пытается это сделать. Он сворачивает прямо перед носом, и прежде чем ты успеваешь среагировать, ваши авто сливаются в один огромный огненный шар.",
          (boldstring) "^^*** Ты погиб. ***";
          deadflag = true;
      }
    }
  ],
  has light;

Object -> car2 "машин/а"
  with name 'машин' 'авто' 'автомобил',
  before [;
    Enter: "Но ты уже в машине!";
  ],
  description "Очень неплохая машина для такого района. Она прямо притягивает взгляд.",
  has scenery female;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Стоянка Лаунгент Технолоджиз
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Object parking_lot "Стоянка Лаунгент Технолоджиз"
 ! with n_to Driveway, !ВРЕМЕННО
  with description "Ты у служебного входа в здание Лаунгент Технолоджиз. Около двери находится прорезь, и табличка с надписью: «ВСТАВЬТЕ КАРТОЧКУ».
  	^^Машина припаркована на стоянке.",
  has light;

Object -> glass_door "двер/ь"
  with name 'двер',
  before [;
    Open: "С этой стороны она открывается автоматически";
  ],
  description "Стеклянная дверь с табличкой: «СЛУЖЕБНЫЙ ВХОД»",
  has lockable locked openable scenery female;

Object -> slot "прорез/ь"
  with name 'прорезь' 'отверстие' 'щель' 'слот',
  before [;
    Receive:
      if (noun ~= ID_card)
        "Она принимает только идентификационные карточки Лаунгент Технолоджиз.";
      else
        print "Загорается зелёный светодиод, дверь со щелчком открывается, и ты входишь внутрь.^";
        PlayerTo(Loungent_Tech);
        return true;
  ],
  description "Отверстие для идентификационных карточек Лаунгент Технолоджиз.",
  has scenery container open female;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Лаунгент Технолоджиз
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Object Loungent_Tech "Лаунгент Технолоджиз"
  with n_to "В сложившихся обстоятельствах, шататься по зданию, болтая с программистами — не самая светлая идея, из посетивших тебя сегодня.",
  s_to parking_lot,
  w_to [;
	  Bowman_room();
	  return true;
  ],
  out_to parking_lot,
  in_to cubicle,
  description "Ты стоишь в коридоре у служебного входа в Лаунгент Технолоджиз. Коридор ведёт на север, в приёмную, и на юг, к парковке. Дверь с табличкой «Мэттью Боуман» расположена на западе.",
  has light;

Object -> back_door "входн/ая дверь"
  with name 'входн' 'дверь',
  before [;
    Open: "Она открывается автоматически.";
  ],
  description "Стеклянная дверь с надписью «ДОХВ ЙЫНБЕЖУЛС».",
  has scenery female;

Object -> Bowman_s_door "дверь Боумэна"
  with name 'двер' 'боумэна',
  before [;
    Open, Enter: Bowman_room(); return true;
  ],
  description "На ней табличка: «Мэттью Боуман».",
  has door openable proper scenery female;

Object -> cubicle "кабинк/а"
  with name 'кабинк' 'кабин',
  after [;
    Enter: print "Ты входишь в кабинку.^"; <<Look>>;
  ],
  description "Стены недостаточно высокие, чтобы обеспечить тебе хоть немного уединения.",
  describe "^Неподалёку стоит небольшая кабинка, подписанная просто «Хэдли».",
  has container enterable open transparent female;

Object -> -> desk "стол/"
  with name 'стол',
  description "Этот дешёвый стол прикреплён к стенкам кабинки.",
  has scenery supporter male;

Object -> -> -> form "бланк/"
  with name 'бланк' 'форму',
  before [;
    Sign:
      if (self hasnt general && pen in player) {
      	give self general;
        "Ты подписываешь бумагу.";
      }
      if (self hasnt general)
        "Тебе нечем подписать.";
      else
        "Ты уже поставил подпись.";
  ],
  description [;
    if (self hasnt general)
      "Бланк с маркировкой «209F». Он заполнен, но не подписан.";
    else
      "Бланк с маркировкой «209F». На нём стоит твоя подпись.";
  ],
  has male;

Object -> -> -> pen "ручк/а"
  with name 'ручк',
  description "Обычная шариковая ручка.",
  has female;

Object -> -> -> note "записк/а"
  with name 'записк',
  description "Ты читаешь: «Хэдли, подпиши форму 209, и НЕМЕДЛЕННО зайди ко мне! МБ».",
  has female;

!============================================================================
! Entry point routines
!============================================================================

[ Initialise;
  print "^^^Телефон звонит.^^
  О нет, сколько же ты проспал?! Конечно, ночь была тяжёлой, но всё же это плохо, очень плохо.^^
  Телефон звонит.^^";

  lookmode = 2;
  SetTime(60 * 9 + 5, 1);

!  location = driveway;
!  move keys to player;
!  rtrue;
  location = bed;
  move soiled_clothing to player;
  move gold_watch to player;
  player.description = PlayerDesc;
];

!============================================================================
! Stanard and extended grammar
!============================================================================

Include "RussiaG";

! для «принять душ»
Verb "прин"
  * cAcc_noun -> UseExtra;

Verb "подписать"
  * cAcc_noun -> Sign;

Verb "умыться"
  * -> Wipe;

Extend only "ех" "езж" first
  * -> Drive;

[ DriveSub;
];
